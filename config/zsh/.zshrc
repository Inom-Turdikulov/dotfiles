#!/usr/bin/env zsh

# We'll handle compinit ourselves...
export ZGEN_AUTOLOAD_COMPINIT=0

# NOTE ZGEN_DIR and ZGEN_SOURCE are forward-declared in modules/shell/zsh.nix
# NOTE Zgen is no longer maintained; zgenom is a maintained fork
[ -d "$ZGEN_DIR" ] || git clone https://github.com/jandamm/zgenom "$ZGEN_DIR"
source $ZGEN_SOURCE

if ! zgenom saved; then
  echo "Initializing zgenom"
  rm -f $ZDOTDIR/*.zwc(N)

  zgenom load junegunn/fzf shell

  zgenom load jeffreytse/zsh-vi-mode
  zgenom load zsh-users/zsh-completions src
  zgenom load zsh-users/zsh-history-substring-search
  zgenom load zdharma-continuum/history-search-multi-word
  zgenom load hlissner/zsh-autopair autopair.zsh
  zgenom load zdharma-continuum/fast-syntax-highlighting

  zgenom save
  zgenom compile $ZDOTDIR
fi

source $ZDOTDIR/config.zsh
if [[ $TERM != dumb ]]; then
  source $ZDOTDIR/keybinds.zsh
  source $ZDOTDIR/completion.zsh
  source $ZDOTDIR/aliases.zsh

  ##
  function _cache {
    command -v "$1" >/dev/null || return 1
    local cache_dir="$XDG_CACHE_HOME/${SHELL##*/}"
    local cache="$cache_dir/$1"
    if [[ ! -f $cache || ! -s $cache ]]; then
      echo "Caching $1"
      mkdir -p $cache_dir
      "$@" >$cache
    fi
    source $cache || rm -f $cache
  }

  # fd > find
  if command -v fd >/dev/null; then
    export FZF_DEFAULT_OPTS="--reverse --ansi"
    export FZF_DEFAULT_COMMAND="fd ."
    export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
    export FZF_ALT_C_COMMAND="fd -t d . $HOME"
  fi
  _cache fasd --init posix-alias zsh-{hook,{c,w}comp{,-install}}

  ## Auto-generated by my nix config
  source $ZDOTDIR/extra.zshrc

  ##
  autoload -Uz compinit && compinit -u -d $ZSH_CACHE/zcompdump
  autopair-init

  # If you have host-local configuration, this is where you'd put it
  [ -f ~/.zshrc ] && source ~/.zshrc
fi
